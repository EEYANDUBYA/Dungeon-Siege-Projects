
Go    m_Go$;
GoMind  m_Mind$;
GoBody  m_Body$;
Job    m_Job$;
Goid  m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//  init

event OnJobInitPointers$( Job job$ )
{
  m_Job$    = job$;
  m_Go$    = job$.Go;
  m_Mind$    = job$.Go.Mind;
  m_Body$    = job$.Go.Body;
}


event OnJobInit$( Job job$ )
{
  OnJobInitPointers$( job$ );

  m_Target$  = job$.GoalObject;

  SetState Talk$;
}


////////////////////////////////////////////////////////////////////////////////
//  face the audience
state Face$
{
  transition
  {
    -> Talk$:  OnWorldMessage( WE_MCP_FACING_LOCKEDON );
  }

  event OnEnterState$
  {
    if( !m_Go$.HasConversation )
    {
      report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
              m_Go$.GetTemplateName,
              m_Go$.Goid );
      SetState Exiting$;
    }
    MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
  }
}


////////////////////////////////////////////////////////////////////////////////
//  blab blab away... pretend you're Darlene
state Talk$
{
  transition
  {
    -> Exiting$:  OnWorldMessage( WE_REQ_TALK_END ) =
    {
      if( m_Go$.Mind.ActorAutoFidgets )
      {
        StartFidgetIfRequired$();
      }
    }
  }

  trigger OnWorldMessage$( WE_ANIM_DONE )
  {
    TryAnim$('talk');
  }

  event OnEnterState$//the bandit quest for the town
  {
    // have they killed the bandit's boss?
    if ( Victory.IsQuestCompleted( "island_town_bandits" , m_Target$) )
    {
      m_Go$.GetConversation.RSSelectConversation( "island_town_guard_captain_quest_after" );
      TryAnim$('talk');
    }
    else if (( Victory.GetQuestOrder( "island_town_bandits" , m_Target$ ) == 0))
    {
      //they need to find the missing detatchment
      //m_Go$.GetConversation.RSSelectConversation( "island_town_guard_captain_quest_0" );
      m_Go$.GetConversation.RSSelectConversation( "a_island_town_guard_captain_quest_before" );
      TryAnim$('talk');
    }
    else if (( Victory.GetQuestOrder( "island_town_bandits" , m_Target$ ) == 1))
    {
      //they found the missing detatchment, now they need to kill the boss
      m_Go$.GetConversation.RSSelectConversation( "island_town_guard_captain_quest_1" );
      TryAnim$('talk');
    }
    else if (( Victory.GetQuestOrder( "island_town_bandits" , m_Target$ ) == 2))
    {
      //they killed the boss, now they need to return to the captain
      m_Go$.GetConversation.RSSelectConversation( "island_town_guard_captain_quest_2" );
      TryAnim$('talk');
        //dropping the reward(s)
      {
        SiegePos SpawnPos$;
        GoCloneReq cloneReq$ = MakeGoCloneReq( "gold" );
        SpawnPos$ = m_Target$.go.placement.position;
        cloneReq$.StartingPos  = SpawnPos$;
        if ( m_Target$.Go.Hasmind )
        {
           if (AIQuery.FindSpotRelativeToSource(m_Target$.Go, 1, 1.5, 2.0, m_Target$.Go.Mind.TempPos1))
           {
              cloneReq$.StartingPos = m_Target$.Go.Mind.TempPos1;
           }
        }
        cloneReq$.SnapToTerrain = true;
        Goid gold$ = GoDb.SCloneGo( cloneReq$ );
        gold$.Go.Aspect.SetGoldValue(500);
      }
      if( m_Target$.IsValid() )
      {
        SiegePos SpawnPos$;
        GoCloneReq cloneReq$ = MakeGoCloneReq( "amulet_island_set_island_ancient" );
        SpawnPos$ = m_Target$.go.placement.position;
        cloneReq$.StartingPos  = SpawnPos$;

        If( m_Target$.Go.Hasmind )
        {
          if( AIQuery.FindSpotRelativeToSource( m_Target$.Go, .25, .5, 2.0, m_Target$.Go.Mind.TempPos1 ) )
          {
            cloneReq$.StartingPos = m_Target$.Go.Mind.TempPos1;
          }
        }
        cloneReq$.SnapToTerrain = true;
        GoDb.SCloneGo( cloneReq$ );
      }
    }
    /*else
    {
      //They don't have the quest yet, so they need it assigned to them
      m_Go$.GetConversation.RSSelectConversation( "a_island_town_guard_captain_quest_before" );
      TryAnim$('talk');
    }*/
    m_Go$.GetConversation.RSActivateDialogue();
  }
}


////////////////////////////////////////////////////////////////////////////////
//  finish blabbing
state Exiting$
{
  event OnEnterState$
  {
    m_Job$.MarkForDeletion();
  }
}